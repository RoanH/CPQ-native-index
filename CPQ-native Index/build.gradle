plugins{
	id 'java-library'
	id 'eclipse'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.github.spotbugs' version '5.0.13'
	id 'jacoco'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['src', 'lib']
		}
	}
	test{
		java{
			srcDirs = ['test']
		}
		resources{
			srcDirs = ['test']
		}
	}
}

test{
	useJUnitPlatform()
	dependsOn 'cleanTest'
	
	testLogging{
		events "passed", "skipped", "failed"
	}
}

spotbugs{
	showProgress = true
	effort = 'max'
	reportLevel = 'low'
	excludeFilter = file("exclude.xml")
}

spotbugsMain{
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

repositories{
	mavenCentral()
}

dependencies{
	//implementation 'dev.roanh.gmark:gmark:1.2'
	implementation files('lib/gmark.jar')
	testImplementation(platform('org.junit:junit-bom:5.8.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

task compileNatives(type: Exec, group: "build", description: "Compile native libraries for the project.") {
	workingDir "$projectDir/native"
	if(System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')){
		commandLine 'cmd', '/c', 'build.bat'
    }else{
		commandLine 'sh', '-c', './build.sh'
	}
}

application{
	mainClassName = 'dev.roanh.cpqindex.Main'
}