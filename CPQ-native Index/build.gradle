plugins{
	id 'eclipse'
	id 'application'
	id 'maven-publish'
	id 'signing'
	id 'com.gradleup.shadow' version '8.3.5'
	id 'edu.sc.seis.launch4j' version '3.0.6'
	id 'com.github.spotbugs' version '6.0.18'
	id 'jacoco'
	id 'org.sonarqube' version '6.1.0.5360'
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

sourceSets{
	main{
		java.srcDirs = ['src']
		resources.srcDirs = ['lib']
	}

	test{
		java.srcDirs = ['test']
		resources.srcDirs = ['test']
	}
}

dependencies{
	implementation 'dev.roanh.gmark:gmark:1.2'
	implementation 'commons-cli:commons-cli:1.5.0'
	testImplementation("org.junit.jupiter:junit-jupiter:5.12.0")
	testImplementation("org.junit.platform:junit-platform-launcher")
}

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	mavenCentral()
}

version = findProperty("refName") ?: 'SNAPSHOT'
if(version.matches("v\\d+\\.\\d+")){
	version = version.substring(1)
}

eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
jacocoTestReport.reports.xml.required = true
sourceCompatibility = 1.21
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
javadoc.options.encoding = 'UTF-8'
group = 'dev.roanh.cpqnativeindex'
ext.artifact = 'cpq-native-index'
shadowJar.archiveVersion = 'v' + version
shadowJar.archiveClassifier = ''
shadowJar.archiveBaseName = 'Index'
application.mainClass = 'dev.roanh.cpqindex.Main'

test{
	useJUnitPlatform()
	dependsOn 'cleanTest'

	testLogging{
		events "passed", "skipped", "failed"
		showStandardStreams = true
	}
}

sonar{
	properties{
		property "sonar.projectKey", "TUe_2imc00_cpq-native-index_660d6924-0276-42c8-bbb6-299d65344839"
		property "sonar.projectName", "CPQ-native Index"
	}
}

spotbugs{
	showProgress = true
	effort = Effort.valueOf('MAX')
	reportLevel = Confidence.valueOf('LOW')
	excludeFilter = file("$rootDir/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask){
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

task compileNatives(type: Exec, group: "build", description: "Compile native libraries for the project."){
	workingDir "$projectDir/native"
	if(System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')){
		commandLine 'cmd', '/c', 'build.bat'
	}else{
		commandLine 'sh', '-c', './build.sh'
	}
}

java{
	withSourcesJar()
	withJavadocJar()
}

publishing{
	publications{
		mavenJava(MavenPublication){
			from components.java
			artifactId = project.artifact

			pom{
				name = project.eclipse.project.name
				description = 'A graph database index with native support for CPQs.'
				url = 'https://github.com/RoanH/CPQ-native-index'
				licenses{
					license{
						name = 'GNU General Public License v3.0'
						url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@github.com:RoanH/CPQ-native-index.git'
					developerConnection = 'scm:git:git@git.roanh.dev:TUe/2imc00/cpq-native-index.git'
					url = 'https://github.com/RoanH/CPQ-native-index'
				}
			}
		}
	}

	repositories{
		maven{
			url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials{
				username findProperty("OSSRH_USER")
				password findProperty("OSSRH_PASS")
			}
		}
	}
}

shadowJar{
	mergeServiceFiles()
	into("META-INF/maven/$project.group/$project.artifact"){
		from{
			generatePomFileForMavenJavaPublication
		}
		rename ".*", "pom.xml"
	}
}

signing{
	useInMemoryPgpKeys(findProperty("signingKeyId"), findProperty("signingKey"), findProperty("signingPassword"))
	sign publishing.publications.mavenJava
}

launch4j{
	jarTask = project.tasks.shadowJar
	mainClassName = application.mainClass
	jreMinVersion = project.sourceCompatibility.toString()
	bundledJrePath = "%JAVA_HOME%"
	outfile = 'Index-v' + project.version + '.exe'
	fileDescription = "CPQ-native Index"
	productName = rootProject.name
	version = project.version
	textVersion = project.version
	copyright = "Roan Hofland"
	headerType = "console"
}
